plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    repositories {
        maven {
            url "https://cursemaven.com"
        }
        gradlePluginPortal()
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

//    modImplementation "curse.maven:cobblemon-687131:4797468"

    modImplementation "curse.maven:ftb-quests-fabric-438496:4760285"
    modImplementation "curse.maven:ftb-teams-fabric-438497:4623115"
    modImplementation "curse.maven:ftb-library-fabric-438495:4720055"
    modImplementation "curse.maven:item-filters-309674:4838265"
    include modApi('teamreborn:energy:3.0.0') {
        transitive = false
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "mod_name": project.mod_name,
                "mod_id": project.mod_id,
                "mod_version": project.version,
                "mod_description": project.mod_description,
                "repository": project.repository,
                "license": project.license,
                "mod_icon": project.mod_icon,
                "environment": project.environment,
                // Below, add all dependencies that you want to add to your mod's fabric.mod.json
                "supported_minecraft_versions": project.supported_minecraft_versions
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

jar {
    archivesBaseName = project.archives_base_name + "-fabric"
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}